buildscript {
    ext {
        kotlin_version = "1.3.21"
        coroutines_version = "1.1.1"
        spek_version = "2.0.1"
        kluent_version = "1.49"
        mockk_version = "1.9"
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
    }
}

allprojects {
    group "io.philarios"

    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/spekframework/spek"
        }
        maven {
            url "https://dl.bintray.com/spekframework/spek-dev"
        }
        maven {
            url "https://dl.bintray.com/markusamshove/maven"
        }
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "jacoco"
    apply from: "${project.rootDir}/bintray.gradle"

    sourceSets.main.java.srcDirs += ["src/generated/kotlin"]

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

        testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
        testImplementation "org.amshove.kluent:kluent:$kluent_version"
        testImplementation "io.mockk:mockk:$mockk_version"
        testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    
    test {
        useJUnitPlatform {
            includeEngines "spek2"
        }
    }

    // FIXME Convert Gradle to Kotlin
    if (project.name != "philarios-core" && project.name != "philarios-jsonschema") {
        apply plugin: "application"
        application {
            mainClassName = "io.${project.name.replace("-", ".")}.cmd.GeneratorKt"
        }
    }
    
}