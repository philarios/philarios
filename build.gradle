buildscript {
    ext {
        kotlin_version = '1.3.0'
        coroutines_version = '1.0.0'
        spek_version = '2.0.0-rc.1'
        kluent_version = '1.42'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
    }
}

allprojects {
    group 'io.philarios'
    version '0.9.3'

    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/spekframework/spek"
        }
        maven {
            url "https://dl.bintray.com/spekframework/spek-dev"
        }
        maven {
            url  "https://dl.bintray.com/markusamshove/maven"
        }
    }

    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.0"
        }
    }
}


subprojects {
    apply plugin: "kotlin-multiplatform"
    apply plugin: "maven-publish"

    publishing {
        repositories {
            maven {
                name "kotlinMultiplatform"
                url "https://api.bintray.com/content/philarios/philarios/${project.name}/$version"
                credentials {
                    username bintrayUser
                    password bintrayKey
                }
            }
            maven {
                name "jvm"
                url "https://api.bintray.com/content/philarios/philarios/${project.name}-jvm/$version"
                credentials {
                    username bintrayUser
                    password bintrayKey
                }
            }
            maven {
                name "metadata"
                url "https://api.bintray.com/content/philarios/philarios/${project.name}-metadata/$version"
                credentials {
                    username bintrayUser
                    password bintrayKey
                }
            }
        }
    }
    afterEvaluate {
        tasks.withType(PublishToMavenRepository).all { publishTask ->
            publishTask.onlyIf { task ->
                if (task.publication.name != task.repository.name) {
                    task.enabled = false
                    task.group = null
                    return false
                }
                return true
            }
        }
    }
}
